options:
    prefix: &4&lBOSS &7| &6
    bossname: &6&lThanos
    loc: 349.999900, 147.000000, 161.500000

on load:
    kill {b}
    delete {b}
    set {bossdrop} to gold nugget named "{@bossname} Fragment"

function boss():
    if {b} is not set:
        summon wither skeleton at location {@loc} in world "world"
        set {b} to last spawned wither skeleton
        set yaw of {b} to 180
        set pitch of {b} to 0
        set helmet of {b} to netherite helmet of protection 6
        set chestplate of {b} to netherite chestplate of protection 6
        set leggings of {b} to netherite leggings of protection 6
        set boots of {b} to netherite boots of protection 6
        set tool of {b} to netherite sword of sharpness 15
        set offhand tool of {b} to netherite sword of sharpness 15
        set max health of {b} to 175
        heal {b}
        set display name of {b} to "{@bossname} &c%health of {b}%/%max health of {b}%❤"
        broadcast "{@prefix}{@bossname} has spawned! &7[&1350, 147, 161&7]"
        play sound "block.end_portal.spawn" to all players

every 2 hours:
	boss()

on damage:
    if victim is {b}:
        set display name of {b} to "{@bossname} &c%health of {b}%/%max health of {b}%❤"

on death:
    if victim is {b}:
        clear drops
        delete {b}
        play sound "block.respawn_anchor.set_spawn" to all players
        broadcast "{@prefix}{@bossname} &6has been killed by &d%attacker%!"
        if attacker is player:
            set {_r} to a random integer between 1 and 3
            loop {_r} times:
                console command "/levelup %attacker%"
            loop {_r}*2 times:
                drop {bossdrop}

on right click:
    if name of event-item is "{@bossname} Key Voucher":
        remove 1 of event-item from player's inventory
        console command "crates givekey %player% boss"

every 5 seconds:
    if {b} is set:
        set {_c} to a random integer between 1 and 5
        if {_c} is 1: #Pull Players
            loop all players in radius 7 of {b}:
                if loop-player's gamemode is survival:
                    push loop-player forward at speed 2
                    apply slowness of tier 5 to loop-player for 4 seconds
                    play sound "entity.llama.swag" to loop-player
        if {_c} is 2: #Wither + Blindness
            loop all players in radius 5 of {b}:
                if loop-player's gamemode is survival:
                    apply wither of tier 2 to loop-player for 1 seconds
                    apply blindness of tier 2 to loop-player for 3 seconds
                    play sound "entity.wither_skeleton.hurt" to loop-player
        if {_c} is 3: #Knockback + Wither:
            loop all players in radius 5 of {b}:
                if loop-player's gamemode is survival:
                    push loop-player up at speed 2
                    push loop-player backwards at speed 2
                    play sound "entity.wither_skeleton.hurt" to loop-player
                    apply wither of tier 2 to loop-player for 2 seconds
        if {_c} is 4: #Syphon
            loop all players in radius 5 of {b}:
                if loop-player's gamemode is survival:
                    damage loop-player by 8 hearts
                    apply blindness of tier 2 to loop-player for 3 seconds
                    play sound "entity.player.attack.crit" to loop-player
                    send "{@prefix}{@bossname} &6has syphoned 3 hearts of your health!" to loop-player
            heal {b} by 15 hearts
        if {_c} is 5: #Buff
            loop all players in radius 6 of {b}:
                if loop-player's gamemode is survival:
                    apply strength of tier 2 to {b} for 3 seconds
                    apply weakness of tier 1 to loop-player for 3 seconds
                    play sound "entity.splash_potion.break" to loop-player
        loop all blocks in radius 7 of {b}:
            if loop-block is cobweb or white wool:
                set loop-block to air